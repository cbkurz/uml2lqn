import "operations.eol";

pre {
	var gqam = UML!ProfileApplication.all
		.collect(i | i.appliedProfile)
		.selectOne(i | i.name = "GQAM");
		
	gqam.println("gqam: ");

	var stereotypes = gqam.getOwnedStereotypes();
	var gaexechost = stereotypes.selectOne(i | i.name = "GaExecHost");
	var gastep = stereotypes.selectOne(i | i.name = "GaStep");
	var gascenario = stereotypes.selectOne(i | i.name = "GaScenario");
	
	var deployment = UML!Package.all.selectOne(i | i.name = "deployment_view");
}

post {}

rule Model2Schema 
	transform uml: UML!Model
	to lqn: LQN!LqnModelType
	{
		guard: uml.name <> "PrimitiveTypes"
		var nodes = UML!Node.all();

		lqn.name = uml.name;
		lqn.processor ::= nodes;
	}

rule Node2Processor
	transform node: UML!Node
	to proc: LQN!ProcessorType 
	{	
		/* 
		 * The guard filters the nodes:
		 * - Nodes thet are not stereotyped with <<GaExecHost>>;
		 * - A <<GaExecHost>> Node without deployments
         * - A <<GaExecHost>> Node deploying elements that are not interacting
		 */
		guard {
			/* 
			 * Gets the components deployed on this Node
			 * Selects components interacting in a scenario
			 */
			var components = node.getDeployed().select(i | i.isInteracting());
			/* 
			 * The transformation considers nodes stereotyped with 
		     * <<GaExecHost>> and deploying interacting components
             */
			return node.getAppliedStereotypes()
				.exists(i|i.name = "GaExecHost") and components.notEmpty();
		}
		var speedFactor = node.getSpeedFactor();
		/* 
		 * Retrieves the components deployed on the current node 
		 * and interacting in a scenario
		 */
		var components = node.getDeployed().select(i | i.isInteracting());

		proc.name = node.name;
		proc.task ::= components;
	}
	
rule Actor2Task
	transform actor: UML!Actor
	to task: LQN!TaskType
	{
		// guard: actor.isInteracting()
		
		var lifelines = actor.getLifelines();
		var beses = lifelines.collect(
			i | i.getElements("BehaviorExecutionSpecification")
		).flatten;

		task.name = actor.name;
		task.scheduling = LQN!TaskSchedulingType#ref;
		task.entry ::= beses;
	}
	
rule Component2Task
	transform component: UML!Component
	to task: LQN!TaskType
	{
		guard {
			/**
			 * The guard filters all the components not interacting and
			 * not deployed on a Node
			 **/
			if(component.isInteracting() and component.getArtifacts().isEmpty())
				throw "Component " + component.name + " is not represented by any Artifact";
				
			return component.isInteracting();
		}

		var lifelines = component.getLifelines();
		lifelines = lifelines.select(i | i.isInteracting());
		
		var beses = lifelines.collect(i | i.getElements("BehaviorExecutionSpecification")).flatten;
		
		// The multiplicity of a task is given by the number of artifacts on which it is deployed
		var artifacts = component.getArtifacts();
		var multiplicity = artifacts.size().asString();
							
		task.name = component.name;
		task.entry ::= beses;
		task.multiplicity = multiplicity.asBigInteger();
	}

rule BES2Entry
	transform bes: UML!BehaviorExecutionSpecification
	to entry: LQN!EntryType
	{	
		guard {
			/**
			 * A BES is excluded from the transformation if:
			 * It doesn't start with a UML!MessageOccurrenceSpecification;
			 * It doesn't end with a UML!MessageOccurrenceSpecification;
			 * It is not bound to a lifeline;
			 */
			if(not (bes.isWellFormed()))
				return false;
			
			// @Assumption: a BES has an exclusive Lifeline
			var lifeline = bes.covered.first;
					
			if(lifeline.isUndefined())
				throw "The BES: " + bes.name + " is not bound to any lifeline";
				
			var type = lifeline.represents.type;
			return type.isInteracting();
		}
		
		var lifeline = bes.covered.first;
		var type = lifeline.represents.type;
		type.println("type: --> ");

		var messages = bes.getMessages();
		// Gets only the messages that are sent
		messages = messages.select(i | i.isSend() and i.message.messageSort <> UML!MessageSort#reply);	
		
		// If msgs is empty, then the BES is a sink 
		if(messages.isEmpty())
			messages.add(bes.start);
	
		entry.name = bes.name;					
		entry.entryPhaseActivities = new LQN!PhaseActivities;
		entry.entryPhaseActivities.activity ::= messages;
	}
	

rule Message2Activity
	transform msgo: UML!MessageOccurrenceSpecification
	to actv: LQN!ActivityPhasesType
	{
		guard {
			/**
			 * A message is not included in the trasformation if not stereotyped with "GaStep"
			 */
			var message = msgo.message;
			return message.checkStereotypeApplication("GaStep");
		}
		
		var message = msgo.message;		
		// Gets the values of the GaStep tags
		var stereotype = message.checkAndGetStereotype("GaStep");
	
		var rep = message.getValue(stereotype, "rep");
		var execTime = message.getValue(stereotype, "execTime").first;
		var hostDemandMean = "1.0";

		if(not execTime.isUndefined()) {
			// Calculates the service demand
			hostDemandMean = execTime.asDouble() * rep.asDouble();			
		}
							
		actv.name = message.name;
		actv.phase = "1".asBigInteger();
		actv.hostDemandMean = hostDemandMean;
	
		// Creates the LQN activity and synch-call tag to the destionation UML!BES
		if(msgo.isSend())
		{		
			var call = new LQN!ActivityMakingCallType;		
			var lifeline = message.receiveEvent.covered;
			
			// Gets the BES starting with the message
			var bes = lifeline.first.getElements("BehaviorExecutionSpecification");
			// Gets the target BES
			bes = bes.selectOne(i | i.start == message.receiveEvent and
				i.finish.isTypeOf(MessageOccurrenceSpecification));
	
			call.dest = bes.name;
			call.callsMean = rep;
			
			if(message.messageSort = "asynchCall")
				actv.synchCall = Sequence{call};
			else
				actv.asynchCall = Sequence{call};
		}
	}